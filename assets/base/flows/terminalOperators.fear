package base.flows

_First[E]: F[mut FlowOp[E], _Sinks, mut Opt[E]]{upstream, sinkFactory -> Block#
  .if {upstream.isFinite.not} .error {TerminalOnInfiniteError#}
  .let res = {Slots#[E]}
  .var[Bool] stopped = {False}
  .do {upstream.for(sinkFactory#{'runner
    .stopDown -> Block#(stopped := True, upstream.stopUp),
    .pushError(info) -> stopped.get ? {.then -> {}, .else -> Error!info},
    #(e) -> Block#
      .do {res.ensureFull{e}}
      .return {runner.stopDown},
    })}
  .do {upstream.stopUp}
  .return {res.opt}
  }

_FindMap[E,R]: F[mut FlowOp[E], read F[E, mut Opt[R]], _Sinks, mut Opt[R]]{upstream, f, sinkFactory -> Block#
  .if {upstream.isFinite.not} .error {TerminalOnInfiniteError#}
  .let res = {Slots#[R]}
  .var[Bool] stopped = {False}
  .do {upstream.for(sinkFactory#{'runner
    .stopDown -> Block#(stopped := True, upstream.stopUp),
    .pushError(info) -> stopped.get ? {.then -> {}, .else -> Error!info},
    #(e) -> f#e.match{
      .some(e') -> Block#(res.ensureFull{e'}, runner.stopDown),
      .empty -> {},
      },
    })}
  .do {upstream.stopUp}
  .return {res.opt}
  }

_Fold[S,E]: F[mut FlowOp[E], S, read F[S,E,S], _Sinks, S]{upstream, acc, f, sinkFactory -> Block#
  .if {upstream.isFinite.not} .error {TerminalOnInfiniteError#}
  .var[S] res = {acc}
  .var[Bool] stopped = {False}
  .do {upstream.for(sinkFactory#[E]{'runner
    .stopDown -> Block#(stopped := True, upstream.stopUp),
    .pushError(info) -> stopped.get ? {.then -> {}, .else -> Error!info},
    #(e) -> res := (f#(res.get, e)),
    })}
  .do {upstream.stopUp}
  .return {res.get}
  }
