package base

Var:{
  #[X](x: X): mut Var[X] -> Block#
    .let[mut _MagicVarImpl[X]] inner = {_MagicVarImpl#x}
    .return {{
      .get -> inner.get,
      .swap(x') -> inner.swap(x'),
      }},
  }
Var[X:imm,mut,read]: Sealed{
  mut  .get: X,
  read .get: read/imm X,
  mut  *: X -> this.get,
  read *: read/imm X -> this.get,

  mut .swap(x: X): X,
  mut :=(x: X): Void -> Block#(this.swap(x)),
  mut .set(x: X): Void -> this := x,
  mut <-(f: mut UpdateVar[X]): X -> this.swap(f#(this*)),
  mut .update(f: mut UpdateVar[X]): X -> this <- f,
  }
UpdateVar[X]: { mut #(x: X): X }

Count[N:imm]: Var[N]{
  mut ++: N,
  mut --: N,
  mut +=(n: N): N,
  mut -=(n: N): N,
  mut *=(n: N): N,
  }
Count:{
  #(n: Int): mut Count[Int] -> this.int(n),
  .int(n: Int): mut Count[Int] -> Let#({
    .var -> Var#n,
    .in(c) -> {
      .get -> c.get,
      .swap(x) -> c.swap(x),
      ++ -> c <- { c' -> c' + +1 },
      -- -> c <- { c' -> c' - +1 },
      +=(n2) -> c <- { c' -> c' + n2 },
      -=(n2) -> c <- { c' -> c' - n2 },
      *=(n2) -> c <- { c' -> c' * n2 },
      }
    }),
  .nat(n: Nat): mut Count[Nat] -> Let#({
    .var -> Var#n,
    .in(c) -> {
      .get -> c.get,
      .swap(x) -> c.swap(x),
      ++ -> c <- { c' -> c' + 1 },
      -- -> c <- { c' -> c' - 1 },
      +=(n2) -> c <- { c' -> c' + n2 },
      -=(n2) -> c <- { c' -> c' - n2 },
      *=(n2) -> c <- { c' -> c' * n2 },
      }
    })
  }

// Var as defined in the formal reduction of Fearless.
// We capture this in the richer Var.
_MagicVarImpl: {#[X](x: X): mut _MagicVarImpl[X] -> Magic!}
_MagicVarImpl[X]: {
  mut  .get: X,
  read .get: read/imm X,
  mut .swap(x: X): X,
  }
