package base.flows

_SeqFlow: {
  .fromOp[E](source: mut FlowOp[E], size: Opt[UInt]): mut Flow[E] -> {'self
    // non-terminals
    .filter(p) -> this.fromOp(_Filter#(source, p), {}),
    .map(f) -> this.fromOp(_Map#(source, f), size),
    .flatMap(f) -> this.fromOp(_FlatMap#(source, f), {}),
    mut .actor[S,R](state: iso S, f: read ActorImpl[S,E,R], onComplete: mut Consumer[S]): mut Flow[imm R] ->
      this.fromOp(_Actor#[S,E,R](source, state, f, onComplete), {}),
    .limit(n) -> this.fromOp(_Limit#(source, n), size),

    // terminals
    mut .findMap[R](f: read F[E, mut Opt[R]]): mut Opt[R] -> Block#
      .if {source.isFinite.not} .error {TerminalOnInfiniteError#}
      .var[mut Ref[mut Opt[R]]] res = {Ref#mut Opt[R]}
      .do {source.forRemaining{'runner
        .stop -> source.stop,
        #(e) -> f#e.match{.some(e') -> Block#(res := (Opt#e'), runner.stop), .empty -> {}},
        }}
      .do {source.stop}
      .return {res.get},

    .find(p) -> self.findMap{e -> p#e ? {.then -> Opt#e, .else -> {}}},
    .any(p) -> self
      .findMap{e -> p#e ? {.then -> Opt#True, .else -> {}}}
      .isSome,
    .all(p) -> self
      .findMap{e -> p#e ? {.then -> {}, .else -> Opt#False}}
      .isEmpty,

//    .fold(acc, f) -> source.step{e -> ControlFlow.continue(e)}.match{
//      .continue(e) -> self.fold(f#(acc, e), f),
//      .continue -> self.fold(acc, f),
//      .break -> acc,
//      .return(e) -> f#(acc, e),
//      },
    mut .fold[S](acc: S, f: read F[S,E,S]): S -> Block#
      .if {source.isFinite.not} .error {TerminalOnInfiniteError#}
      .var[mut Ref[S]] acc' = {Ref#acc}
      .do {source.forRemaining{
        .stop -> source.stop,
        #(e) -> acc' := (f#(acc'.get, e)),
        }}
      .do {source.stop}
      .return {acc'.get},
    // Seq flows do not split so no combine is needed
    .fold(acc, f, combine) -> self.fold(acc, f),
//      .return{self.forRemainingSeq[S] mut _Sink[E, S]{
//        mut #(e: E): mut ControlFlow[S] -> Block#(acc' := (f#(acc'.get, e)), ControlFlow.continue[S]),
//        read #(e: E): mut ControlFlow[S] -> Error.str "unreachable",
//        }.match[S]{
//          .continue -> acc'.get,
//          .break -> acc'.get,
//          .return(_) -> acc'.get,
//          }},

    .size -> size.match[UInt]{
      .some(n) -> n,
    // TODO: inference broken on .empty, see the test `branchingReturnTypes`
//      .empty -> self.fold[UInt](0u, {acc, _ -> acc + 1u}),
      .empty -> Block#
        .var[UInt] start = {0u}
        .return {self.fold(start, {acc, _ -> acc + 1u})}
      },
    },

  #[E](e: E): mut Flow[E] -> Block#
    .var[mut Ref[mut Opt[E]]] seq = { Ref#(Opt#e) }
    .var[mut FlowOp[E]] source = {{ 'self
      .isRunning -> seq.get.isSome,
      .stop -> seq := {},
      .step(downstream) -> seq.swap(mut Opt[E]).match{
        .some(x) -> downstream#x,
        .empty -> Block#(downstream.stop, self.stop),
        },
      .forRemaining(downstream) -> seq.swap(mut Opt[E]).match{
        .some(x) -> Block#(downstream#x, downstream.stop, self.stop),
        .empty -> Block#(downstream.stop, self.stop),
        },
      }}
    .return {this.fromOp(source, Opt#[UInt]1u)},
//    .return {this.fromOp(source, Opt#1u)},
  }
