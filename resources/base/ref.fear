package base

Ref:{ #[X](x: mdf X): mut Ref[mdf X] -> this#(x) }
Ref[X]:NoMutHyg[X],Sealed{
  read *: recMdf X,
  read .get: recMdf X -> this*,
  lent .swap(x: mdf X): mdf X,
  lent :=(x: mdf X): Void -> Yeet#(this.swap(x)),
  lent .set(x: mdf X): Void -> this := x,
  lent <-(f: mut UpdateRef[mdf X]): mdf X -> this.swap(f#(this*)),
  lent .update(f: mut UpdateRef[mdf X]): mdf X -> this <- f,
  }
UpdateRef[X]:NoMutHyg[mdf X]{ mut #(x: mdf X): mdf X }

Count[N]:Ref[N]{
  lent ++: N,
  lent --: N,
  lent +=(n: N): N,
  lent -=(n: N): N,
  lent *=(n: N): N,
  }
Count:{
  #(n: Int): mut Count[Int] -> this.int(n),
  .int(n: Int): mut Count[Int] -> Let#{
    .var -> Ref#n,
    .in(c) -> {
      * -> c*,
      .swap(x) -> c.swap(x),
      ++ -> c <- { c' -> c' + 1 },
      -- -> c <- { c' -> c' - 1 },
      +=(n2) -> c <- { c' -> c' + n2 },
      -=(n2) -> c <- { c' -> c' - n2 },
      *=(n2) -> c <- { c' -> c' * n2 },
      }
    },
  .uint(n: UInt): mut Count[UInt] -> Let#{
    .var -> Ref#n,
    .in(c) -> {
      * -> c*,
      .swap(x) -> c.swap(x),
      ++ -> c <- { c' -> c' + 1u },
      -- -> c <- { c' -> c' - 1u },
      +=(n2) -> c <- { c' -> c' + n2 },
      -=(n2) -> c <- { c' -> c' - n2 },
      *=(n2) -> c <- { c' -> c' * n2 },
      }
    }
  }
