package base

Opt:{ recMdf #[T](x: recMdf T): recMdf Opt[mdf T] -> {
  .match(m) -> m.some(x),
  .matchHyg(m) -> m.some(x),
  }}
Opt[T]:{
  recMdf .match[R](m: mut OptMatch[recMdf T, mdf R]): mdf R -> m.none,
  recMdf .matchHyg[R](m: recMdf OptMatchHyg[recMdf T, mdf R]): mdf R -> m.none,
  recMdf !: recMdf T -> this.match(mut _OptUnwrap[recMdf T]),
  recMdf .map[R](f: mut OptMap[recMdf T, mdf R]): mut Opt[mdf R] -> this.match(f),
  recMdf .flatMap[R](f: mut OptFlatMap[recMdf T, mdf R]): mut Opt[mdf R] -> this.match(f),
  recMdf .filter(f: mut OptMap[recMdf T, Bool]): recMdf Opt[recMdf T] -> this._filter(this.map(f) | False),
    recMdf ._filter(res: Bool): recMdf Opt[recMdf T] ->
      res.look(recMdf BoolView[recMdf Opt[recMdf T]]{ .then -> this, .else -> {} }),
  recMdf .do(f: mut OptMatch[recMdf T, Void]): recMdf Opt[recMdf T] ->
    Yeet.with[Void, recMdf Opt[recMdf T]](this.match(f), this),
  recMdf |(alt: recMdf T): recMdf T -> this || (recMdf OptOr#alt),
  recMdf ||(alt: recMdf OptOr[recMdf T]): recMdf T -> this.matchHyg(alt),
  read .isNone: Bool -> this.match{ .none -> True, .some(_) -> False },
  read .isSome: Bool -> this.match{ .none -> False, .some(_) -> True },
  }
OptMatch[T,R]:{ mut .some(x: mdf T): mdf R, mut .none: mdf R }
OptMatchHyg[T,R]:{ recMdf .some(x: mdf T): mdf R, recMdf .none: mdf R }
OptMap[T,R]:OptMatch[mdf T, mut Opt[mdf R]]{
  mut #(x: mdf T): mdf R,
  .some(x) -> mut Opt#(this#x),
  .none -> {},
  }
OptFlatMap[T,R]:OptMatch[mdf T, mut Opt[mdf R]]{
  mut #(x: mdf T): mut Opt[mdf R],
  .some(x) -> this#x,
  .none -> {},
  }

OptOr:{ recMdf #[T](alt: recMdf T): recMdf OptOr[recMdf T] -> { alt } }
OptOr[T]:OptMatchHyg[mdf T, mdf T]{ recMdf .some(x: mdf T): mdf T -> x }

_OptUnwrap[T]:OptMatch[mdf T, mdf T]{ .some(x) -> x, .none -> Abort! }
